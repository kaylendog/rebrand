plugins {
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "com.diffplug.spotless" version "6.10.0"
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    // cloth api
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.terraformersmc.com" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    // cloth api
    modApi("me.shedaniel.cloth:cloth-config-fabric:8.1.77") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // mod menu
    modImplementation "com.terraformersmc:modmenu:4.0.6"
    // toml library
    include "com.moandjiezana.toml:toml4j:0.7.2"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 16
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

spotless {
    java {
        eclipse()
        licenseHeader("/*\n" +
                " * Copyright Kaylen Dart 2022\n" +
                " * This project is licensed under the GNU GPLv3 license. See the LICENSE file for more information.\n" +
                " */")
    }
}